// cDictionary class v1.01:
// Created by Erik Svensson and published on eriksven.com in February 2013
// License: LGPLv3 - GNU LESSER GENERAL PUBLIC LICENSE Version 3 - http://www.gnu.org/licenses/lgpl.txt

Define cDICTIONARY_INCLUDED for 1

Define Scripting.Dictionary for "{EE09B103-97E0-11CF-978F-00A02463E06F}"

Class cDictionary is a DFComAutomationObject
    Procedure Construct_Object
        Forward Send Construct_Object
        Set psProgID to Scripting.Dictionary
        Set peAutoCreate to 1
    End_Procedure

    { Visibility=Public }
    Function Is_cDictionary Returns Boolean
        Function_Return True
    End_Function
    
    Procedure Set Value Variant vKey Variant vValue
        Handle hDispatchDriver
        Get phDispatchDriver to hDispatchDriver
        Send PrepareParams to hDispatchDriver 2
        Send DefineParam to hDispatchDriver (|CI$4000 ior 12) vKey
        Set ComProperty of hDispatchDriver 0 (|CI$4000 ior 12) to vValue
    End_Procedure
    
    Function Value Variant vKey Returns Variant
        Handle hDispatchDriver
        Variant retVal
        Get phDispatchDriver to hDispatchDriver
        Send PrepareParams to hDispatchDriver 1
        Send DefineParam to hDispatchDriver (|CI$4000 ior 12) vKey
        Get InvokeComMethod of hDispatchDriver 0 12 to retVal
        Function_Return retVal
    End_Function
    
    Function KeyExists Variant vKey Returns Boolean
        Handle hDispatchDriver
        Boolean retVal
        Get phDispatchDriver to hDispatchDriver
        Send PrepareParams to hDispatchDriver 1
        Send DefineParam to hDispatchDriver (|CI$4000 ior 12) vKey
        Get InvokeComMethod of hDispatchDriver 3 11 to retVal
        Function_Return retVal
    End_Function
    
    Procedure DeleteKey Variant vKey
        Handle hDispatchDriver
        Get phDispatchDriver to hDispatchDriver
        Send PrepareParams to hDispatchDriver 1
        Send DefineParam to hDispatchDriver (|CI$4000 ior 12) vKey
        Send InvokeComMethod to hDispatchDriver 7 24
    End_Procedure
    
    Function Keys Returns Variant[]
        Variant[] va
        Handle hDispatchDriver
        Variant retVal
        Get phDispatchDriver to hDispatchDriver
        Get InvokeComMethod of hDispatchDriver 6 12 to retVal
        Move retVal to va
        Function_Return va
    End_Function
End_Class